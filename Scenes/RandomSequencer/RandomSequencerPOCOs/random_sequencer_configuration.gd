extends Resource
class_name RandomSequenceConfiguration

@export var entries:Array[RandomSequencerEntry] = []
@export var overrides:Array[RandomSequenceOverride]


## Caches the overrides of this sequencer, and gathers them by the element
var overrides_by_element:Dictionary[Variant, Array] = {}
## Caches the elements that can be generated by this secuencer
var elements:Array[Resource] = [] : get = get_elements

## Organizes the overrides so that there is no need to iterate through 
## the overrides list every time.
func cache_overrides()-> void:		
	if not overrides_by_element.is_empty(): return # already cached
	
	for override in overrides:
		if override.element not in overrides_by_element:
			var o:Array[RandomSequenceOverride] = []
			overrides_by_element[override.element] = o
		overrides_by_element[override.element].append(override)
		
		
func get_element_overrides(element:Variant)-> Array[RandomSequenceOverride]:
	cache_overrides()
	
	if element not in overrides_by_element.keys():
		return []		
	
	return overrides_by_element[element] as Array[RandomSequenceOverride]


## Returns the elements that can be generated with this sequencer
func get_elements()-> Array[Resource]:
	# Cache the elements the first time
	if elements.is_empty():
		for entry in entries:
			elements.append(entry.element)
			
	# Return the cache
	return elements
